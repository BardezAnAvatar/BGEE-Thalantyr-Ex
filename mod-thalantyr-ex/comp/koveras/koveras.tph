
//make cursed ring
COPY_EXISTING ~RING25.ITM~ ~override/BZRNG1.ITM~ //Koveras Gift, cursed
    //Patches
        SAY NAME @300
        SAY NAME2 @301
        SAY DESC @302
        SAY IDENTIFIED_DESC @303
        WRITE_LONG 0x34 300                     // Price
        WRITE_LONG 0x18 0b111100                // flags: 2-6, 4 being cursed
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 0                // AC Bonus
                parameter1 = "-3"
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 33               // Save: Death
                parameter1 = "-3"
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 34               // Save: Wands
                parameter1 = "-3"
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 35               // Save: Polymorph
                parameter1 = "-3"
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 36               // Save: Breath
                parameter1 = "-3"
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 37               // Save: Spell
                parameter1 = "-3"
        END
        LPF ~ADD_ITEM_EQEFFECT~
            INT_VAR
                opcode = 278            // Type: THAC0 bonus
                target = 1              // Self
                timing = 2              // Timing Mode: Instant/While Equipped
                parameter1 = "-3"       // value to modify
                parameter2 = 0          // flags for what kind of value to set; 0 = cumulative
                power = 0
                resist_dispel = 0       // Natural/Nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 0
                insert_point = "-1"     // add to end
            STR_VAR
                resource = ~~           // unused for opcode
        END
        LPF ~ADD_ITEM_EQEFFECT~
            INT_VAR
                opcode = 142            // Type: Display Portrait Icon
                target = 1              // Self
                timing = 2              // Timing Mode: Instant/While Equipped
                parameter1 = 0          // unused 
                parameter2 = 35         // Icon: Cursed
                power = 0
                resist_dispel = 0       // Natural/Nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 0
                insert_point = "-1"     // add to end
            STR_VAR
                resource = ~~           // unused for opcode
        END